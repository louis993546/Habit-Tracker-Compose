plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.squareup.sqldelight'
    id 'com.diffplug.spotless'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.louis993546.habittracker"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

kotlin {
    sourceSets.all {
        languageSettings.enableLanguageFeature('InlineClasses')
    }
}


spotless {
    ratchetFrom 'origin/main'
    // https://github.com/diffplug/spotless/issues/704
    // https://github.com/pinterest/ktlint/issues/813
    kotlin {
        target '**/*.kt'
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint("0.37.2")
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    format 'xml', {
        target '**/*.xml'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

dependencies {
    // Basic Android stuff
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0"

    // Compose stuff from Google
    implementation "androidx.activity:activity-compose:1.3.0-alpha02"
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha07"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Other useful stuff
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.1.1"
    implementation "com.squareup.sqldelight:android-driver:1.4.3"

    // Testing stuff
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}